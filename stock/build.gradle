plugins {
	id 'buildlogic.java-spring-conventions'

	// spring
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'

	// spotless
	id "com.diffplug.spotless" version "7.0.0.BETA4"
}

dependencies {
	// Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// ________________ Dependencies ________________
	// Reactive & Network
	implementation libs.rxjava
	implementation libs.bundles.retrofit
	// OpenAPI Documentation
	implementation libs.springdoc.openapi.starter

	// ________________ Project dependencies ________________
	implementation project(':shared:spring-base')

	// Development
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	// Database
	runtimeOnly 'org.postgresql:postgresql'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('dev') {
	doFirst {
		// Start continuous build in background
		ProcessBuilder buildProcess = new ProcessBuilder(
				isWindows() ? 'gradlew.bat' : './gradlew',
				':stock:build',
				'--continuous'
		)
		buildProcess.redirectOutput(ProcessBuilder.Redirect.INHERIT)
		buildProcess.redirectError(ProcessBuilder.Redirect.INHERIT)
		buildProcess.start()
	}

	finalizedBy(':stock:bootRun')
}

static def isWindows() {
	return System.getProperty('os.name').toLowerCase().contains('windows')
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '.gitattributes', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		target '**/src/**/*.java'

		// Use the default importOrder configuration
		importOrder()

		// Cleanthat will refactor your code, but it may break your style: apply it before your formatter
		cleanthat()          // has its own section below

		// formatters.
		palantirJavaFormat()

		formatAnnotations()  // fixes formatting of type annotations, see below

		licenseHeader '/* (C) $YEAR */' // or licenseHeaderFile
	}
}
